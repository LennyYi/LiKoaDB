IF EXISTS (SELECT * FROM SYSOBJECTS WHERE ID = OBJECT_ID('DBO.USPGNOVAGENBILLNO') AND SYSSTAT & 0xF = 4)
	DROP PROCEDURE DBO.USPGNOVAGENBILLNO
GO

/*******************************************************************
	COMPASS 2000 USER STORED PROCEDURE

	USPGNOVAGENBILLNO.SQL - to generate modal bill number
        
    PROCESSING DETAILS:			
	AUTHOR      :     Hinson Liang
	DATE	    :     11/26/2014
	PIRNO	    :	  

	REVISION LOG:
	VERSION	  PIRNO		PROGRAMMER	REMARK		DATE		PURPOSE
********************************************************************/
create proc dbo.USPGNOVAGENBILLNO
(
     @cCLNTCODE		CHAR(05),
	 @dtBILLPRDFR	DATETIME,	 
     @cBILLNO		CHAR(08) OUTPUT,
	 @nERROR		INT OUTPUT
)
as

DECLARE @cPGMNAME     VARCHAR(50)
DECLARE @cErrMsg      VARCHAR(65)
DECLARE @cErrData     VARCHAR(45)
DECLARE @cNUM1 CHAR(01)
DECLARE @cNUM2 CHAR(02)
DECLARE @cNUM1_NEW CHAR(01)
DECLARE @cNUM2_NEW CHAR(02)
DECLARE @BILLNO_12	CHAR(02)
DECLARE @BILLNO_34	CHAR(02)
DECLARE @BILLNO_56	CHAR(02)
DECLARE @BILLNO_78	CHAR(02)
DECLARE @cTEMP	CHAR(02)
DECLARE @nNUM	INT

SELECT @cBILLNO = ''
SELECT @BILLNO_78 = '00'
SELECT @cPGMNAME = 'USPGNOVAGENBILLNO'
SELECT @nERROR = 0

BEGIN TRY
	--Generate number list
	CREATE TABLE #BILLSEQ(
		IDX INT	NOT NULL IDENTITY (1, 1),
		NUM CHAR(01)
	)

	SELECT @nNUM = ASCII('0')
	WHILE @nNUM <= ASCII('9')
	BEGIN
		INSERT #BILLSEQ (NUM) VALUES(CHAR(@nNUM))
		SELECT @nNUM = @nNUM + 1
	END

	SELECT @nNUM = ASCII('A')
	WHILE @nNUM <= ASCII('Z')
	BEGIN
		INSERT #BILLSEQ (NUM) VALUES(CHAR(@nNUM))
		SELECT @nNUM = @nNUM + 1
	END

	--Bill number 5 ~ 6
	SELECT @cTEMP = ISNULL(RTRIM(BILLSEQNUM),'') FROM TCLIENT WHERE CLNTCODE = @cCLNTCODE
	IF @cTEMP = ''
	BEGIN
		SELECT @BILLNO_56 = '00'
	END
	ELSE IF ISNUMERIC(@cTEMP) = 1
	BEGIN
		IF CONVERT(INT,@cTEMP) = 99
		BEGIN
			SELECT @BILLNO_56 = 'A0'
		END
		ELSE
		BEGIN
			SELECT @BILLNO_56 = RIGHT('0' + CONVERT(VARCHAR(02),CONVERT(INT,@cTEMP)+1),2)
		END
	END
	ELSE
	BEGIN
		SELECT @cNUM1 = SUBSTRING(@cTEMP,1,1), @cNUM2 = SUBSTRING(@cTEMP,2,1)
		IF @cNUM1 = 'Z' AND @cNUM2 = 'Z'	--No avaible bill number
		BEGIN
			SELECT @nERROR = -1
		END
		ELSE IF @cNUM2 = 'Z'
		BEGIN
			SELECT @cNUM1_NEW = NUM FROM #BILLSEQ A WHERE A.IDX = (SELECT MAX(IDX) + 1 FROM #BILLSEQ WHERE NUM = @cNUM1)
			SELECT @cNUM2_NEW = '0'
			SELECT @BILLNO_56 = @cNUM1_NEW + @cNUM2_NEW
		END
		ELSE
		BEGIN
			SELECT @cNUM1_NEW = @cNUM1
			SELECT @cNUM2_NEW = NUM FROM #BILLSEQ A WHERE A.IDX = (SELECT MAX(IDX) + 1 FROM #BILLSEQ WHERE NUM = @cNUM2)
			SELECT @BILLNO_56 = @cNUM1_NEW + @cNUM2_NEW
		END
	END
	
		
	IF @nERROR = 0
	BEGIN
		--Bill number 1 ~ 2
		SELECT @cTEMP = RIGHT('0' + CONVERT(VARCHAR(02),DATEPART(YY,@dtBILLPRDFR) - 2000),2)
		SELECT @BILLNO_12 = CHAR(ASCII(SUBSTRING(@cTEMP,1,1)) + 17) + SUBSTRING(CONVERT(CHAR(04),DATEPART(YY,@dtBILLPRDFR)),4,1)
		
		--Bill number 3 ~ 4
		SELECT @BILLNO_34 = RIGHT('0' + CONVERT(VARCHAR(02),DATEPART(MM,@dtBILLPRDFR)),2)
		
		--Bill number
		SELECT @cBILLNO = @BILLNO_12 + @BILLNO_34 + @BILLNO_56 + @BILLNO_78
	END
	ELSE
	BEGIN
		SELECT @cBILLNO = ''
	END
	
	DROP TABLE #BILLSEQ

END TRY
BEGIN CATCH
	SELECT @nERROR = error_number() 
	--SELECT error_message() 
	SELECT @cBILLNO = ''
	
END CATCH

--Update bill number if no error
IF @nERROR = 0
BEGIN
	UPDATE TCLIENT SET BILLSEQNUM = @BILLNO_56 WHERE CLNTCODE = @cCLNTCODE
END

